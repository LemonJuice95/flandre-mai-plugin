plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'maven-publish'
    id 'org.jreleaser' version '1.20.0'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withSourcesJar()
    withJavadocJar()
}

group = 'io.github.lemonjuice95'
version = '0.8.0'

jreleaser {
    signing {
        active = 'ALWAYS'
        armored = true
        mode = 'FILE'
        publicKey = rootProject.findProperty("gpg.publicKey")
        secretKey = rootProject.findProperty("gpg.secretKey")
        passphrase = rootProject.findProperty("gpg.passphrase")
    }

    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')

                    username = rootProject.findProperty("gpg.mavenUsername")
                    password = rootProject.findProperty("gpg.mavenPassword")
                }
            }
        }
    }
}

publishing {
    publications {
        maven (MavenPublication) {
            groupId = 'io.github.lemonjuice95'
            artifactId = 'flandre-mai-plugin'
            version = project.version

            from components.java

            pom {
                name = 'Flandre Mai Plugin'
                description = 'A plugin of MaiMaiDX related functions for io.github.lemonjuice95:flandre-bot-framework'
                url = 'https://github.com/LemonJuice95/flandre-mai-plugin'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/license/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'lemonjuice95'
                        name = 'LemonJuice95'
                        email = '1582017385@qq.com'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/LemonJuice95/flandre-mai-plugin'
                    developerConnection = 'scm:git:ssh://github.com/LemonJuice95/flandre-mai-plugin'
                    url = 'https://github.com/LemonJuice95/flandre-mai-plugin'
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}
repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    //lombok
    compileOnly 'org.projectlombok:lombok:1.18.36'
    annotationProcessor 'org.projectlombok:lombok:1.18.36'

    compileOnly 'io.github.lemonjuice95:flandre-bot-framework:0.5.1'

    //Http Request
    implementation 'org.apache.httpcomponents:httpclient:4.5.14'
}



test {
    useJUnitPlatform()
}